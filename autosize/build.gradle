apply plugin: 'com.android.library'
apply from: 'publish.gradle'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        versionCode VERSION_CODE
        versionName VERSION_NAME

        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
        warning 'InvalidPackage'
    }
}

dependencies {
    compileOnly rootProject.appcompat_v7
}

static def generateVersionCode() {
    def strTime = new Date().format("yyyyMMddHH", TimeZone.getTimeZone("GMT+8"))
    return Integer.parseInt(strTime)
}

task deleteMediateJar(type: Delete) {
    delete file('build/intermediates/others/classes')
    doLast {
        // 删除临时文件
        println('Delete temp files of Others')
    }
}

task deleteOriJar(type: Delete) {
    delete file('build/intermediates/packaged-classes/release/classes.jar')
    doLast {
        // 删除临时文件
        println('Delete ori jar')
    }
}

task copyJar2Mediate(type: Copy, dependsOn: [deleteMediateJar]) {
    from zipTree('build/intermediates/packaged-classes/release/classes.jar')
    into new File('build/intermediates/others/classes')
    doLast {
        println('Unzip and copy **.jar to \'build/intermediates/others/classes\'')
    }
}

// 自动生成autosize的jar
// 命令：gradlew :autosize:makeJar
task makeJar(type: Jar, dependsOn: [copyJar2Mediate, 'assembleRelease', deleteOriJar]) {
    // 导出的jar文件名称
    archiveFileName = 'autosize_' + generateVersionCode() + '.jar'
    // 从哪个目录打包jar
    from('build/intermediates/others/classes')
    // 导出的jar文件的存放目录（未指定则默认存放在build/libs下）
    destinationDirectory = file('build/libs')
    // 去掉不要的类，
    // exclude('com/android') // jar不含有类路径com.android
    // exclude('com/android/*') // jar含有类路径com.android
    // exclude('android')
    // 需要打包的类
    // include('com/**/*.class')
    doLast {
        println('MakeJar ' + archiveFileName + ' to \'build/libs\'')
    }
}